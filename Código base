//Exportar las librerías
#include <Servo.h>
#include <LiquidCrystal_I2C.h>

//Definir los servos
Servo base;
Servo LI;
Servo LD;
Servo garra;

//Definir los pines de los potenciómetros
int pot_base=A3;
int pot_LI=A2;
int pot_LD=A1;
int pot_garra=A0;

//Definir los botones y switch
const int btt_fwd= 4;
const int btt_bwd= 2;
const int modo= 7;

// Anti-rebote
bool anti_avance = LOW;
bool anti_retroceso = LOW;
const unsigned long debounceTime = 10;  // Tiempo de pulsación válido
unsigned long previousMillisdebounce = 0;

//Configuración de la pantalla
LiquidCrystal_I2C lcd(0x27, 16,2);

//Prototipo de funciones
void movimiento (int valor1, int valor2, int valor3, int valor4);
void ImprimirPosicion (int pos1, int pos2, int pos3, int pos4);

//definir estados
typedef enum estados {s0, s1, s2, s3, s4, s5} estados;
estados estadoActual = s0;
estados estadoFuturo = s0;

//Temporización
const long  tiempodeVentana= 1000;  //Tiempo que se tardará en cambiar en ms.
unsigned long previousMillis = 0; //Almacena la última marca de tiempo para el control no bloqueante

void setup() {
lcd.init(); //Inicializa el modulo adaptadorel cual internamente configura e inicializa el I2C y el LCD.
lcd.begin(16, 2);
lcd.backlight(); //Enciende la luz de fondo

// Inicialización del puerto serial
Serial.begin(9600);

//Configuracion de los servos
base.attach(11);
LI.attach(10);
LD.attach(9);
garra.attach(3);

//Configuracion de los pines
pinMode(btt_fwd, INPUT);
pinMode(btt_bwd, INPUT);
pinMode(modo, INPUT);

}

void loop() {
//Lectura del modo y botones
bool avance = digitalRead(btt_fwd);
bool retroceso = digitalRead(btt_bwd);
bool mode = digitalRead(modo);

//Tiempos de ventana para avance y retroceso
bool flanco_fwd = (avance == LOW && anti_avance == HIGH);
bool flanco_bwd = (retroceso == LOW && anti_retroceso == HIGH);

//Tiempo de lectura
unsigned long currentMillisdebounce = millis();
if (currentMillisdebounce - previousMillisdebounce >= debounceTime){
  anti_avance = avance;
  anti_retroceso = retroceso;
  previousMillisdebounce = currentMillisdebounce;
}

//Lectura y mapeo de los potenciómetros
int val_base = map(analogRead(pot_base),0,1023,0,180); 
int val_LI = map(analogRead(pot_LI),0,1023,0,180); 
int val_LD = map(analogRead(pot_LD),0,1023,0,180); 
int val_garra = map(analogRead(pot_garra),0,1023,0,180);

switch (estadoActual){
    case s0: 
      if(mode==HIGH)
        estadoFuturo = s1;
      else if (mode==LOW)
        estadoFuturo = s0;
      break;
    case s1:
       if(mode==LOW)
        estadoFuturo = s0;
       else if(flanco_fwd==HIGH && mode==HIGH && flanco_bwd==LOW)
        estadoFuturo = s2;
       else if(flanco_fwd==LOW && mode==HIGH && flanco_bwd==HIGH)
        estadoFuturo = s5;
      break;
    case s2:
      if(mode==LOW)
        estadoFuturo = s0;   
      else if(flanco_fwd==HIGH && mode==HIGH && flanco_bwd==LOW)
        estadoFuturo = s3;
       else if(flanco_fwd==LOW && mode==HIGH && flanco_bwd==HIGH)
        estadoFuturo = s1;
      break;
     case s3:
      if(mode==LOW)
        estadoFuturo = s0;   
     else if(flanco_fwd==HIGH && mode==HIGH && flanco_bwd==LOW)
        estadoFuturo = s4;
       else if(flanco_fwd==LOW && mode==HIGH && flanco_bwd==HIGH)
        estadoFuturo = s2;
      break;
     case s4:
      if(mode==LOW)
        estadoFuturo = s0;   
      else if(flanco_fwd==HIGH && mode==HIGH && flanco_bwd==LOW)
        estadoFuturo = s5;
       else if(flanco_fwd==LOW && mode==HIGH && flanco_bwd==HIGH)
        estadoFuturo = s3;
      break;
     case s5:
      if(mode==LOW)
        estadoFuturo = s0;   
      else if(flanco_fwd==HIGH && mode==HIGH && flanco_bwd==LOW)
        estadoFuturo = s1;
       else if(flanco_fwd==LOW && mode==HIGH && flanco_bwd==HIGH)
        estadoFuturo = s4;
       break;
  }

//Control de salidas según el estado actual
  switch (estadoActual){
    case s0:
      movimiento (val_base, val_LI, val_LD, val_garra);
      lcd.setCursor(5, 0);
      lcd.print("Manual");
      Serial.print("Posición del servo de la base:");
      ImprimirPosicion (val_base, val_LI, val_LD, val_garra);
      break;
    case s1: 
      movimiento (10, 30, 30, 100);
      lcd.setCursor(3, 0);
      lcd.print("Automatico");
      lcd.setCursor(2, 1);
      lcd.print("Posicion: 01");
      ImprimirPosicion (10, 30, 30, 100);
      break;
    case s2: 
      movimiento (50, 80, 80, 120);
      lcd.setCursor(3, 0);
      lcd.print("Automatico");
      lcd.setCursor(2, 1);
      lcd.print("Posicion: 02");
      ImprimirPosicion (50, 80, 80, 120);
      break;
    case s3: 
      movimiento (70, 100, 100, 45);
      lcd.setCursor(3, 0);
      lcd.print("Automatico");
      lcd.setCursor(2, 1);
      lcd.print("Posicion: 03");
      ImprimirPosicion (70, 100, 100, 45);
      break;
    case s4: 
      movimiento (30, 150, 150, 180);
      lcd.setCursor(3, 0);
      lcd.print("Automatico");
      lcd.setCursor(2, 1);
      lcd.print("Posicion: 04");
      ImprimirPosicion (30, 150, 150, 180);
      break;
    case s5: 
      movimiento (10, 75, 75, 20);
      lcd.setCursor(3, 0);
      lcd.print("Automatico");
      lcd.setCursor(2, 1);
      lcd.print("Posicion: 05");
      ImprimirPosicion (10, 75, 75, 20);
      break;
  }

// actualización de estado
  unsigned long currentMillis = millis();
  if (currentMillis - previousMillis >= tiempodeVentana) {
    estadoActual = estadoFuturo;
    previousMillis = currentMillis;
  }

//Pausa de estabilidad
delay(1);

}

//Definición de la función de movimiento general
void movimiento (int valor1, int valor2, int valor3, int valor4){ 
base.write(valor1);
LI.write(valor2); 
LD.write(valor3);
garra.write(valor4);
}
//Definición de la función de impresion de las posiciones
void ImprimirPosicion (int pos1, int pos2, int pos3, int pos4){
  Serial.print("Posición del servo de la base:");
  Serial.println(pos1);
  Serial.print("Posición del servo del lado izquierdo:");
  Serial.println(pos2);
  Serial.print("Posición del servo del lado derecho:");
  Serial.println(pos3);
  Serial.print("Posición del servo de la garra:");
  Serial.println(pos4);
}
